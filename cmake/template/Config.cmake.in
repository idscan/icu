@PACKAGE_INIT@

include("${CMAKE_CURRENT_LIST_DIR}/@TARGETS_EXPORT_NAME@.cmake")
check_required_components("@PROJECT_NAME@")

function(__icu_set_pkgdata_path)
  if(EXISTS "${ICU_PKGDATA_EXECUTABLE}")
    return()
  endif()

  if(NOT TARGET ICU::pkgdata)
    message(FATAL_ERROR "Internal error")
  endif()

  get_target_property(configurations ICU::pkgdata IMPORTED_CONFIGURATIONS)
  if(NOT configurations)
    message(FATAL_ERROR "Internal error")
  endif()
  list(LENGTH configurations len)
  if(len EQUAL "0")
    message(FATAL_ERROR "Internal error")
  endif()

  list(FIND configurations "RELEASE" release_index)
  if(release_index EQUAL "-1")
    list(GET configurations 0 use_config)
  else()
    set(use_config RELEASE)
  endif()

  get_target_property(
      ICU_PKGDATA_EXECUTABLE ICU::pkgdata IMPORTED_LOCATION_${use_config}
  )

  if(NOT EXISTS "${ICU_PKGDATA_EXECUTABLE}")
    message(FATAL_ERROR "Internal error")
  endif()

  set(ICU_PKGDATA_EXECUTABLE "${ICU_PKGDATA_EXECUTABLE}" PARENT_SCOPE)
endfunction()

function(__icu_set_icupkg_path)
  if(EXISTS "${ICU_ICUPKG_EXECUTABLE}")
    return()
  endif()

  if(NOT TARGET ICU::icupkg)
    message(FATAL_ERROR "Internal error")
  endif()

  get_target_property(configurations ICU::icupkg IMPORTED_CONFIGURATIONS)
  if(NOT configurations)
    message(FATAL_ERROR "Internal error")
  endif()
  list(LENGTH configurations len)
  if(len EQUAL "0")
    message(FATAL_ERROR "Internal error")
  endif()

  list(FIND configurations "RELEASE" release_index)
  if(release_index EQUAL "-1")
    list(GET configurations 0 use_config)
  else()
    set(use_config RELEASE)
  endif()

  get_target_property(
      ICU_ICUPKG_EXECUTABLE ICU::icupkg IMPORTED_LOCATION_${use_config}
  )

  if(NOT EXISTS "${ICU_ICUPKG_EXECUTABLE}")
    message(FATAL_ERROR "Internal error")
  endif()

  set(ICU_ICUPKG_EXECUTABLE "${ICU_ICUPKG_EXECUTABLE}" PARENT_SCOPE)
endfunction()

if(@ICU_BUILD_TOOLS@)
  __icu_set_pkgdata_path()
  __icu_set_icupkg_path()
endif()

if(@ICU_DATA_ARCHIVE_MODE@)
  get_filename_component(
      ICU_DATA_FILE
      "${CMAKE_CURRENT_LIST_DIR}/../../../@ICU_DATA_INSTALL_DIR@/@ICU_DATA_FILE_NAME@"
      ABSOLUTE
  )

  if(NOT EXISTS ${ICU_DATA_FILE})
    message(FATAL_ERROR "File not exists: ${ICU_DATA_FILE}")
  endif()

  if(IS_DIRECTORY ${ICU_DATA_FILE})
    message(FATAL_ERROR "Is directory: ${ICU_DATA_FILE}")
  endif()
endif()
